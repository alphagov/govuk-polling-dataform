config {
    type: "table",
    database: "govuk-polling",
    schema: "synthetic_polling",
    assertions: {
        uniqueKey: ["survey_response_id"],
        nonNull: ["survey_response_id", "survey_wave_id", "src_response_id"]
    },
}

SELECT
  src.survey_response_id,
  src.survey_wave_id,
  src.src_response_id,
  src.date,
  src.weight,
  src.imd_quartile_country,
  lookup_gender.gender,
  lookup_age_group.age_group,
  lookup_qualification.qualification,
  lookup_ethnicity.ethnicity
FROM
  ${ref("stg_bmg_all_waves")} src
LEFT JOIN
  ${ref("survey_waves")} survey_waves
  ON src.wave_name = survey_waves.name
LEFT JOIN
  ${ref("lookup_age_group")} lookup_age_group
USING
    (provider, age_code)
LEFT JOIN
  ${ref("lookup_gender")} lookup_gender
USING
  (provider, gender_code)
LEFT JOIN
  ${ref("lookup_ethnicity")} lookup_ethnicity
USING
  (provider, ethnicity_code)
LEFT JOIN
  ${ref("lookup_qualification")} lookup_qualification
USING
  (provider, qualification_code)
