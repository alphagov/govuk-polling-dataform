/**
Excluded from nonNull Assertions:
1) age_group is excluded from the nonNull test as there are some records in the source with missing values for this column
2) is_english_main_language is excluded as wave 8 does not have a lang is_english_main_language value.
3) ethnicity is excluded as wave 5 does not have an ethnicity value.
**/
config {
    type: "table",
    database: "govuk-polling",
    schema: "govuk_polling_responses",
    assertions: {
        uniqueKey: ["survey_response_id"],
        nonNull: ["survey_response_id", "survey_wave_id", "src_response_id", "weight", "gender", "government_office_region"]
        //  "age_group" can be null
        //  "is_english_main_language" can be null
        //  "ethnicity" can be null
        //  "qualification" can be null (wave 4)
        //  "imd_quartile_country"
    },
}

SELECT
  src.survey_response_id,
  src.survey_wave_id,
  src.src_response_id,
  src.weight,
  src.imd_quartile_country,
  lookup_gender.gender,
  lookup_age_group.age_group,
  lookup_qualification.qualification,
  lookup_ethnicity.ethnicity,
  lookup_government_office_region.government_office_region,
  lookup_is_english_main_language.is_english_main_language
FROM
  ${ref("stg_bmg_all_waves")} src
LEFT JOIN
  ${ref("survey_waves")} survey_waves
  ON src.wave_name = survey_waves.name
LEFT JOIN
  ${ref("lookup_age_group")} lookup_age_group
USING
    (provider, age_code)
LEFT JOIN
  ${ref("lookup_gender")} lookup_gender
USING
  (provider, gender_code)
LEFT JOIN
  ${ref("lookup_ethnicity")} lookup_ethnicity
USING
  (provider, ethnicity_code)
LEFT JOIN
  ${ref("lookup_qualification")} lookup_qualification
USING
  (provider, qualification_code)
LEFT JOIN
  ${ref("lookup_government_office_region")} lookup_government_office_region
USING
  (provider, government_office_region_code)
LEFT JOIN
  ${ref("lookup_is_english_main_language")} lookup_is_english_main_language
USING
  (provider, is_english_main_language_code)
