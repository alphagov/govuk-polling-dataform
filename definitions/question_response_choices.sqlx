config {
    type: "table",
    database: "govuk-polling",
    schema: "synthetic_polling",
    assertions: {
        uniqueKey: ["question_response_choice_id"],
        nonNull: ["question_response_choice_id", "choice_value", "choice_text"]
    },
}
SELECT
    TO_HEX(MD5(CONCAT(question_responses.question_response_id, response_choices.question_id, response_choices.src_question_response_column_name))) AS question_response_choice_id,
    question_responses.question_response_id,
    response_choices.src_question_response_column_name,
    response_choices.choice_value,
    response_choices.choice_text
FROM ${ref("stg_unpack_question_responses")} response_choices
LEFT JOIN ${ref("question_responses")} question_responses
    USING (survey_response_id, question_id)
